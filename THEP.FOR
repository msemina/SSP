C
C     ..................................................................
C
C        SUBROUTINE THEP
C
C        PURPOSE
C           A SERIES EXPANSION IN HERMITE POLYNOMIALS WITH INDEPENDENT
C           VARIABLE X IS TRANSFORMED TO A POLYNOMIAL WITH INDEPENDENT
C           VARIABLE Z, WHERE X=A*Z+B
C
C        USAGE
C           CALL THEP(A,B,POL,N,C,WORK)
C
C        DESCRIPTION OF PARAMETERS
C           A     - FACTOR OF LINEAR TERM IN GIVEN LINEAR TRANSFORMATION
C           B     - CONSTANT TERM IN GIVEN LINEAR TRANSFORMATION
C           POL   - COEFFICIENT VECTOR OF POLYNOMIAL (RESULTANT VALUE)
C                   COEFFICIENTS ARE ORDERED FROM LOW TO HIGH
C           N     - DIMENSION OF COEFFICIENT VECTOR POL AND C
C           C     - COEFFICIENT VECTOR OF GIVEN EXPANSION
C                   COEFFICIENTS ARE ORDERED FROM LOW TO HIGH
C                   POL AND C MAY BE IDENTICALLY LOCATED
C           WORK  - WORKING STORAGE OF DIMENSION 2*N
C
C        REMARKS
C           COEFFICIENT VECTOR C REMAINS UNCHANGED IF NOT COINCIDING
C           WITH COEFFICIENT VECTOR POL.
C           OPERATION IS BYPASSED IN CASE N LESS THAN 1.
C           THE LINEAR TRANSFORMATION X=A*Z+B OR Z=(1/A)(X-B) TRANSFORMS
C           THE RANGE (-C,C) IN X TO THE RANGE (ZL,ZR) IN Z WHERE
C           ZL=-(C+B)/A AND ZR=(C-B)/A.
C           FOR GIVEN ZL, ZR AND C WE HAVE A=2C/(ZR-ZL) AND
C           B=-C(ZR+ZL)/(ZR-ZL)
C
C        SUBROUTINES AND FUNCTION SUBPROGRAMS REQUIRED
C           NONE
C
C        METHOD
C           THE TRANSFORMATION IS BASED ON THE RECURRENCE EQUATION
C           FOR HERMITE POLYNOMIALS H(N,X)
C           H(N+1,X)=2*(X*H(N,X)-N*H(N-1,X)),
C           WHERE THE FIRST TERM IN BRACKETS IS THE INDEX
C           THE SECOND IS THE ARGUMENT.
C           STARTING VALUES ARE H(0,X)=1,H(1,X)=2*X.
C           THE TRANSFORMATION IS IMPLICITLY DEFINED BY MEANS OF
C           X=A*Z+B TOGETHER WITH
C           SUM(POL(I)*Z**(I-1), SUMMED OVER I FROM 1 TO N)
C           =SUM(C(I)*H(I-1,X), SUMMED OVER I FROM 1 TO N).
C
C     ..................................................................
C
      SUBROUTINE THEP(A,B,POL,N,C,WORK)
C
      DIMENSION POL(1),C(1),WORK(1)
C
C        TEST OF DIMENSION
      IF(N-1)2,1,3
C
C        DIMENSION LESS THAN 2
    1 POL(1)=C(1)
    2 RETURN
C
    3 XD=A+A
      X0=B+B
      POL(1)=C(1)+C(2)*X0
      POL(2)=C(2)*XD
      IF(N-2)2,2,4
C
C        INITIALIZATION
    4 WORK(1)=1.
      WORK(2)=X0
      WORK(3)=0.
      WORK(4)=XD
      FI=2.
C
C        CALCULATE COEFFICIENT VECTOR OF NEXT HERMITE POLYNOMIAL
C        AND ADD MULTIPLE OF THIS VECTOR TO POLYNOMIAL POL
      DO 6 J=3,N
      P=0.
C
      DO 5 K=2,J
      H=P*XD+WORK(2*K-2)*X0-FI*WORK(2*K-3)
      P=WORK(2*K-2)
      WORK(2*K-2)=H
      WORK(2*K-3)=P
    5 POL(K-1)=POL(K-1)+H*C(J)
      WORK(2*J-1)=0.
      WORK(2*J)=P*XD
      FI=FI+2.
    6 POL(J)=C(J)*WORK(2*J)
      RETURN
      END
